#include <WiFiClient.h>
#include <ESP8266WebServer.h>
#include <ESP8266mDNS.h> 
#include <wpa2_enterprise.h> //to allow connection to wpa2 enterprise networks
#include <MPU6050_6Axis_MotionApps20.h>
#include <Wire.h>
#include "WiFiLocation.h" //defines MAX_NETWORKS, MAX_SAMPLES
#include "serv_credentials.h" //defines HOMESSID, HOMEPASS, ENTSSID, ENTUSER, ENTPASS
#include "model.h" //classifier model generated by ./generate_model.py
#include "dataset_networks.h" //pushes networks in the classifier dataset into known_networks in order

String message[MAX_SAMPLES];
int message_index = 0;
double RSSI_arr[MAX_NETWORKS];

MPU6050 mpu; // IMU
#define INTERRUPT_PIN D0  
bool blinkState = false;

// MPU control/status vars
bool dmpReady = false; // set true if DMP init was successful
uint8_t mpuIntStatus; // holds actual interrupt status byte from MPU
uint8_t devStatus; // return status after each device operation (0 = success, !0 = error)
uint16_t packetSize; // expected DMP packet size (default is 42 bytes)
uint16_t fifoCount; // count of all bytes currently in FIFO
uint8_t fifoBuffer[64]; // FIFO storage buffer

Quaternion q; // [w, x, y, z] quaternion container

volatile bool mpuInterrupt = false; // indicates whether MPU interrupt pin has gone high
void ICACHE_RAM_ATTR dmpDataReady( void )
{
    mpuInterrupt = true;
}

ESP8266WebServer server(80);

void setup()
{
    char* ssid = NULL;
    char* username = NULL;
    char* password = NULL;

    Serial.begin(115200);
    Serial.println("");
    pinMode(LED_BUILTIN, OUTPUT);
    digitalWrite(LED_BUILTIN, LOW);

    Serial.print("Searching for network to connect to:");
    while( ssid == NULL )
    {
        int network_count = WiFi.scanNetworks();
        for( int i = 0; i < network_count; i++ )
        {
            Serial.print(".");
            if( WiFi.SSID(i).equals(HOMESSID) )
            {
                Serial.println("[HOME network found]");
                ssid = HOMESSID;
                password = HOMEPASS;
                break;
            }
            if( WiFi.SSID(i).equals(ENTSSID) )
            {
                Serial.println("[ENTERPRISE network found]");
                ssid = ENTSSID;
                username = ENTUSER;
                password = ENTPASS;
          
                //WPA2-enterprise PEAP setup
                struct station_config wifi_config;
                memset(&wifi_config, 0, sizeof(wifi_config));

                strcpy((char*)wifi_config.ssid, ssid);

                wifi_set_opmode(STATION_MODE);
                wifi_station_set_config(&wifi_config);
                wifi_station_clear_cert_key();
                wifi_station_clear_enterprise_ca_cert();
                wifi_station_set_wpa2_enterprise_auth(1);
                wifi_station_set_enterprise_identity((uint8*)username, strlen(username));
                wifi_station_set_enterprise_username((uint8*)username, strlen(username));
                wifi_station_set_enterprise_password((uint8*)password, strlen(password));
                wifi_station_connect();
                break;
            }
        }
        if( ssid == NULL )
        {
            Serial.print("Couldn't find home or enterprise network");
            delay(5000); //Neither home or enterprise network found
        }
    }

    Serial.print("Connecting to network:");
    WiFi.mode(WIFI_STA);
    WiFi.begin(ssid,password);
    while( WiFi.status() != WL_CONNECTED )
    {
        delay(500);
        Serial.print(".");
    }    
    digitalWrite(LED_BUILTIN, HIGH);
    
    Serial.println("");
    Serial.print("Connected to ");
    Serial.println(ssid);
    Serial.print("IP address: ");
    Serial.println(WiFi.localIP());

    Wire.begin();
    Wire.setClock(400000);
    
    Serial.println(F("Initializing I2C devices..."));
    mpu.initialize();
    pinMode(INTERRUPT_PIN, INPUT);

    Serial.println(mpu.testConnection() ? F("MPU6050 connection successful") : F("MPU6050 connection failed"));

    // load and configure the DMP
    Serial.println(F("Initializing DMP..."));
    devStatus = mpu.dmpInitialize();
    
    // supply your own gyro offsets here, scaled for min sensitivity
    mpu.setXGyroOffset(220);
    mpu.setYGyroOffset(76);
    mpu.setZGyroOffset(-85);
    mpu.setZAccelOffset(1788);

    if( devStatus == 0 )
    {
        // Calibration Time: generate offsets and calibrate our MPU6050
        mpu.CalibrateAccel(6);
        mpu.CalibrateGyro(6);
        // turn on the DMP, now that it's ready
        Serial.println();
        Serial.println(F("Enabling DMP..."));
        mpu.setDMPEnabled(true);

        // enable Arduino interrupt detection
        Serial.print(F("Enabling interrupt detection (Arduino external interrupt "));
        Serial.print(digitalPinToInterrupt(INTERRUPT_PIN));
        Serial.println(F(")..."));
        attachInterrupt(digitalPinToInterrupt(INTERRUPT_PIN), dmpDataReady, RISING);
        mpuIntStatus = mpu.getIntStatus();

        // set our DMP Ready flag so the main loop() function knows it's okay to use it
        Serial.println(F("DMP ready! Waiting for first interrupt..."));
        dmpReady = true;

        // get expected DMP packet size for later comparison
        packetSize = mpu.dmpGetFIFOPacketSize();
    }else
    {
        // 1 = initial memory load failed
        // 2 = DMP configuration updates failed
        Serial.print(F("DMP Initialization failed (code "));
        Serial.print(devStatus);
        Serial.println(F(")"));
    }
    
    if( MDNS.begin("esp8266") )
    {
        Serial.println("mDNS responder started");
    }

    server.on("/", handle_index);
    server.on("/scan_networks", scan_networks);
    server.on("/take_measurement", take_measurement);
    server.on("/predict_location", predict_location);
    server.on("/read_IMU", read_IMU);

    add_dataset_networks();

    server.begin();
    Serial.println("HTTP server started");
}

void loop()
{
    server.handleClient();
    MDNS.update();
}

void handle_index()
{
    digitalWrite(LED_BUILTIN, LOW);
    String temp = "<html>\n\    <head>\n\
        <title>ESP8266 WiFiLocation test</title>\n\
    <\head>\n    <body>\n\
        <a href=\"/scan_networks\" style=\"font-size:100px\">scan networks</a><br>\n\
        <a href=\"/take_measurement\" style=\"font-size:100px\">take measurement</a><br>\n\
        <a href=\"/predict_location\" style=\"font-size:100px\">predict location</a><br>\n\
        <a href=\"/read_IMU\" style=\"font-size:100px\">read IMU</a>\n\
    </body>\n</html>";
    server.send(200, "text/html", temp);
    digitalWrite(LED_BUILTIN, HIGH);
}

void take_measurement()
{
    scan_RSSIs();
    print_RSSIs();
}

void scan_RSSIs()
{
    digitalWrite(LED_BUILTIN, LOW);
    int network_count = WiFi.scanNetworks();
    int rssi;

    reset_RSSIs();

    for( int i = 0; i < network_count; i++ )
    {
        String bssid = WiFi.BSSIDstr(i);
        uint16_t network_index = known_networks.indexOf(bssid);

        if( network_index != 65535 )
        {
            rssi = WiFi.RSSI(i);
            RSSI_arr[network_index] = WiFi.RSSI(i);
        }
    }
    digitalWrite(LED_BUILTIN, HIGH);
}

void print_RSSIs()
{
    digitalWrite(LED_BUILTIN, LOW);
    int known_net_count = known_networks.length();
    String temp;

    if( message_index < MAX_SAMPLES )
    {
        mpu.dmpGetCurrentFIFOPacket(fifoBuffer);
        mpu.dmpGetQuaternion(&q, fifoBuffer);
        message[message_index] = "";
        message[message_index] += q.w;
        message[message_index] += ", ";
        message[message_index] += q.x;
        message[message_index] += ", ";
        message[message_index] += q.y;
        message[message_index] += ", ";
        message[message_index] += q.z;
        message[message_index] += ", ";
        for( int i = 0; i < known_net_count; i++ )
        {
            if( RSSI_arr[i] == 0.00 )
            {
                RSSI_arr[i] = -100;
            }
            message[message_index] += RSSI_arr[i];
            if( i != known_net_count - 1 )
            {
                message[message_index] += ',';
            }
        }
        message_index++;
    }
    
    temp = "<html>\n    <head>\n";
    if( message_index < MAX_SAMPLES )
    {
        temp += "        <meta http-equiv='refresh' content='0'/>\n";
    }
    temp += "        <title>ESP8266 WiFiLocation test</title>\n\
    </head>\n    <body>\n\
        <a href=\"/\" style=\"font-size:100px\">\<-back</a><br>\n        <p>";
    for( int i = 0; i < known_net_count; i++ )
    {
        temp += known_networks[i];
        temp += i == known_net_count - 1 ? "</p>\n" : ",";
    }
    temp += "        <p>";
    for( int i = 0; i < message_index; i++ )
    {
        temp += message[i]+"<br>\n";
    }
    temp += "</p>    </body>\n</html>";

    if( message_index == MAX_SAMPLES )
    {
        digitalWrite(LED_BUILTIN, HIGH);
        for( int i = 0; i < 5; i++ )
        {
            delay(50);
            digitalWrite(LED_BUILTIN, LOW);
            delay(50);
            digitalWrite(LED_BUILTIN, HIGH);
        }
        reset_RSSIs();
        message_index = 0;
    }

    server.send(2000, "text/html", temp);
}

void reset_RSSIs()
{
    const uint16_t RSSI_count = sizeof(RSSI_arr) / sizeof(double);

    for( int i = 0; i < RSSI_count; i++ )
    {
        RSSI_arr[i] = 0;
    }
}

void scan_networks()
{
    digitalWrite(LED_BUILTIN, LOW);
    int network_count = WiFi.scanNetworks();
    String temp, bssid;

    message_index = 0;

    for( int i = 0; i < network_count; i++ )
    {
        bssid = WiFi.BSSIDstr(i);
        message[i] = bssid;
        message[i] += " (";
        message[i] += WiFi.RSSI(i); 
        message[i] += ") - ";
        message[i] += WiFi.SSID(i);

        if( WiFi.SSID(i).equals("student-curtin") || 
            WiFi.SSID(i).equals("eduroam") ||
            WiFi.SSID(i).equals("staff-curtin") ||
            WiFi.SSID(i).equals("CurtinGuest") ||
            WiFi.SSID(i).equals("Kottler") ||
            WiFi.SSID(i).equals("Gkiphone") ||
            WiFi.SSID(i).equals("GkiPhone6") ||
            WiFi.SSID(i).equals("WAVLINK-N") )
        {
            uint16_t network_index = known_networks.indexOf(bssid);
            if( network_index == 65535 )
            {
                known_networks.push(bssid);
            }
        }
    }
    int known_net_count = known_networks.length();
    
    temp = "<html>\n    <head>\n\
        <title>ESP8266 WiFiLocation test</title>\n\
    </head>\n    <body>\n\
        <a href=\"/\" style=\"font-size:100px\">\<-back</a><br>\n        <p>";
    for( int i = 0; i < network_count; i++ )
    {
        temp += message[i]+"<br>\n";
        message[i] = "";
    }
    temp += "</p>\n        <p>";
    for( int i = 0; i < known_net_count; i++ )
    {
        temp += known_networks[i];
        temp += i == known_net_count - 1 ? "</p>\n" : ",";
    }
    temp += "    </body>\n</html>";

    server.send(400, "text/html", temp);
    digitalWrite(LED_BUILTIN, HIGH);
}

void predict_location()
{
    digitalWrite(LED_BUILTIN, LOW);
    String temp;

    scan_RSSIs();

    temp = "<html>\n    <head>\n\
        <title>ESP8266 WiFiLocation test</title>\n\
    </head>\n    <body>\n\
        <a href=\"/\" style=\"font-size:100px\">\<-back</a><br>\n        <p>";
    temp += classIdxToName(predict(RSSI_arr));
    temp += "</p>\n    </body>\n</html>";

    server.send(400, "text/html", temp);
    digitalWrite(LED_BUILTIN, HIGH);
}

void read_IMU( void )
{
    digitalWrite(LED_BUILTIN, LOW);
    String temp;
    
    if( !dmpReady ) return;

    if( mpu.dmpGetCurrentFIFOPacket(fifoBuffer) )
    {
        mpu.dmpGetQuaternion(&q, fifoBuffer);

        temp = "<html>\n    <head>\n\
            <title>ESP8266 WiFiLocation test</title>\n\
        </head>\n    <body>\n\
            <a href=\"/\" style=\"font-size:100px\">\<-back</a><br>\n        <p>";
        temp += "q: ";
        temp += q.w;
        temp += ", ";
        temp += q.x;
        temp += ", ";
        temp += q.y;
        temp += ", ";
        temp += q.z;
        temp += "</p>\n    </body>\n</html>";

        server.send(400, "text/html", temp);
    }

    digitalWrite(LED_BUILTIN, HIGH);
}
